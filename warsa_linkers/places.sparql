PREFIX text: <http://jena.apache.org/text#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX hipla: <http://ldf.fi/schema/hipla/>
PREFIX places: <http://ldf.fi/warsa/places>
PREFIX wsc: <http://ldf.fi/schema/warsa/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?id ?label ?ngram ?type
{
  {
    VALUES ?ngram {
      <VALUES>
    }
    BIND(CONCAT('"',REPLACE(?ngram,"([\\+\\-\\&\\|\\!\\(\\)\\{\\}\\[\\]\\^\\\"\\~\\*\\?\\:\\/\\\\])","\\\\$1"),'"') AS ?qstring)
    ?id text:query ?qstring .
    VALUES ?g {
      <http://ldf.fi/warsa/places/karelian_places>
      <http://ldf.fi/warsa/places/municipalities>
    }
    GRAPH ?g {
      VALUES ?ngram {
        <VALUES>
      }
      ?id rdfs:label|skos:prefLabel|skos:altLabel ?label ;
                                   a ?type .
      FILTER(str(?label) = ?ngram)
    }
    MINUS {
      ?id a wsc:Man-made_feature .
    }
    MINUS {
      ?id a wsc:Symbol .
    }
  }
  UNION
  {
    SERVICE <http://172.18.0.3:3030/pnr/sparql> {
      VALUES ?ngram {
        <VALUES>
      }
      BIND(CONCAT('"',REPLACE(?ngram,"([\\+\\-\\&\\|\\!\\(\\)\\{\\}\\[\\]\\^\\\"\\~\\*\\?\\:\\/\\\\])","\\\\$1"),'"') AS ?qstring)
      ?id text:query ?qstring .
      VALUES ?type {
        <http://ldf.fi/pnr-schema#place_type_560>
        <http://ldf.fi/pnr-schema#place_type_550>
        <http://ldf.fi/pnr-schema#place_type_540>
      }
      ?id a ?type .
      ?id rdfs:label|skos:prefLabel ?label .
      FILTER(STR(?label)=STR(?ngram))
    }
  }
}
ORDER BY ?id
