PREFIX text: <http://jena.apache.org/text#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX actors: <http://ldf.fi/warsa/actors/>
PREFIX atypes: <http://ldf.fi/warsa/actors/actor_types/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
SELECT DISTINCT ?id ?label ?ngram ?war {
  VALUES ?ngram {
    <VALUES>
  }
  FILTER(STRLEN(?ngram)>2)
  {
    VALUES ?ngram {
      <VALUES>
    }
    BIND(REPLACE(?ngram,'\\.$', '') AS ?qlabel)
    BIND(CONCAT('"',REPLACE(?qlabel,"([\\+\\-\\&\\|\\!\\(\\)\\{\\}\\[\\]\\^\\\"\\~\\*\\?\\:\\/\\\\])","\\\\$1"),'"') AS ?qstring)
    GRAPH <http://ldf.fi/warsa/actors> { ?rid text:query ?qstring . }
  } UNION {
    VALUES ?ngram {
      <VALUES>
    }
    BIND(REPLACE(?ngram,'\\.$', '') AS ?qlabel)
    ?rid actors:covernumber ?qlabel .
    BIND(?rid AS ?id)
  }
  {
    ?rid a/rdfs:subClassOf* atypes:MilitaryUnit .
    BIND(?rid AS ?id)
  }
  UNION
  {
    ?rid crm:P95_has_formed ?id .
  }
  ?rid rdfs:label|skos:prefLabel|skos:altLabel|actors:covernumber ?label .

  FILTER(LCASE(REPLACE(STR(?label), "[,\\s]", ""))=LCASE(REPLACE(STR(?qlabel), "[,\\s]", '')))
  OPTIONAL { ?id <http://ldf.fi/warsa/actors/hasConflict> ?war . }
}
